[[business:Default]]
[role=group,includesConcepts="business:Subdomain,business:SubdomainCrossfunctional,business:BusinessDependency,business:SubdomainOverride,business:SubdomainFriction,business:TechnicalAspects,business:TechnicalAspectsDependency"]
== Business

This section describes the application from the business' perspective.

=== Concepts

==== Subdomains
The application consists of several business subdomains that can be identified by naming conventions.

[[business:Subdomain]]
.Creates predefined Subdomain nodes and connects them to all Type nodes via naming conventions.
[source,cypher,role=concept]
----
UNWIND [
    { name: "Clinic" },
    { name: "Owner" },
    { name: "Person" }, 
    { name: "Pet" },
    { name: "Specialty" },
    { name: "Vet" }, 
    { name: "Visit" }
]
AS properties
MERGE (s:Business:Subdomain{name:properties.name})
WITH s
    MATCH (t:Type)<-[:CONTAINS*]-(:Project)
        WHERE t.name CONTAINS s.name
    MERGE (t)-[:BELONGS_TO]->(s)
RETURN s.name as Subdomain, COUNT(t) as Types
----

==== Crossfunctional Aspects
Some types are not specific to a subdomain, but are there for crossfunctional aspects.
[[business:SubdomainCrossfunctional]]
.Mark all types for the current Project that are crossfunctional.
[source,cypher,role=concept,requiresConcepts="business:Subdomain"]
----
MATCH (t:Type)<-[:CONTAINS*]-(:Project)
WHERE NOT (t)-[:BELONGS_TO]->(:Business:Subdomain)
MERGE (s:Business:Subdomain {name: "crossfunctional"})
MERGE (s)<-[:BELONGS_TO]-(t)
RETURN s.name as CrossfunctionalSubdomain, count(t) as Types
----

==== Subdomain Dependencies

There are dependencies between business sub domains.

[[business:BusinessDependency]]
.Creates DEPENDS_ON relations between two subdomains if there are Java type dependencies between them.
[source,cypher,role=concept,requiresConcepts="business:SubdomainCrossfunctional"]
----
MATCH
  (t1:Type)-[:BELONGS_TO]->(s1:Business:Subdomain),
  (t2:Type)-[:BELONGS_TO]->(s2:Business:Subdomain),
  (t1)-[d:DEPENDS_ON]->(t2)
WITH
  s1, s2, count(d) as weight
MERGE
  (s1)-[d:DEPENDS_ON]->(s2)
SET
  d.weight = weight
RETURN
  s1.name as Business, collect(s2.name) as Dependencies
ORDER BY
  Business
----

==== Subdomain Override
Code can be mark in the application with @ActualSubdomain so signal that a class might be tagged wrongly by using the naming convention from above.
[[business:SubdomainOverride]]
.Find all annotation on types and change the relationship to subdomains accordingly.
[source,cypher,role=concept,requiresConcepts="business:Subdomain"]
----
MATCH
   (a:Artifact { name: "spring-petclinic"})-[:CONTAINS*]->
      (t:Type)-[:ANNOTATED_BY]->(annotation:Annotation)-[:OF_TYPE]->
         (annotationType:Type { name: "ActualSubdomain"}),
            (annotation)-[:HAS]->(v:Value)
OPTIONAL MATCH
   (t)-[b:BELONGS_TO]->(wrong:Business:Subdomain)
DELETE b
MERGE (t)-[:BELONGS_NOT_TO]->(wrong)
WITH t, v, wrong
MATCH (s:Business:Subdomain {name: v.value})
MERGE (t)-[:BELONGS_TO]->(s)
RETURN DISTINCT t.name, s.name as newSubdomain, wrong.name as oldSubdomain
----

==== List wrongly associated classes

[[business:SubdomainFriction]]
.Finds and calculates the friction for wrongly named or placed types within subdomains.
[source,cypher,role=concept,requiresConcepts="business:SubdomainOverride"]
----
MATCH (t:Type)-[ok:BELONGS_TO]->(s:Business:Subdomain)
OPTIONAL MATCH (t)-[nok:BELONGS_NOT_TO]->(other:Business:Subdomain)
RETURN s.name as Subdomain, COUNT(ok) as OK , COUNT(nok) as NOK, COUNT((nok)) / tofloat((COUNT(ok) + COUNT(nok))) as Friction
----

==== Technical Aspects

The application consists of several business sub domains that can be identified by package naming conventions.

[[business:TechnicalAspects]]
.Creates information about technical aspects using package naming conventions.
[source,cypher,role=concept]
----
MATCH (p:Package)-[:CONTAINS]->(t:Type)<-[:CONTAINS*]-(:Project)
WITH t, p.name as packageName 
MERGE (ta:Business:TechnicalAspect {name: packageName})
MERGE (ta)<-[:IS_A]-(t)
RETURN ta.name as TechnicalAspect, count(t) as Types
----

==== Dependencies between technical aspects

There are dependencies between business technical aspects.

[[business:TechnicalAspectsDependency]]
.Creates DEPENDS_ON relations between two subdomains if there are Java type dependencies between them.
[source,cypher,role=concept,requiresConcepts="business:TechnicalAspects"]
----
MATCH
  (t1:Type)-[:IS_A]->(ta1:Business:TechnicalAspect),
  (t2:Type)-[:IS_A]->(ta2:Business:TechnicalAspect),
  (t1)-[d:DEPENDS_ON]->(t2)
WHERE
  ta1 <> ta2
WITH
  ta1, ta2, count(d) as weight
MERGE
  (ta1)-[d:DEPENDS_ON]->(ta2)
SET
  d.weight = weight
RETURN
  ta1.name as TechnicalAspect, collect(ta2.name) as Dependencies
ORDER BY
  TechnicalAspect
----

