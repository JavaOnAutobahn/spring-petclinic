[[business:Default]]
[role=group,includesConcepts="business:Domain,business:Subdomain,business:BusinessDependency,business:TechnicalAspects,business:TechnicalAspectsDependency,business:AlternativeModularization"]
== Business

This section describes the application from the business' perspective.

=== Concepts

==== Domain
The domain of the application is a pet clinic.

[[business:Domain]]
.Mark all source code files that belong to the domain.
[source,cypher,role=concept]
----
MATCH (t:Type)
WHERE t.fqn starts with "org.springframework.samples.petclinic"
SET t:Business:Domain
SET t.domain = "Pet Clinic"
RETURN COUNT(t)
----

==== Subdomains
The application consists of several business and technical subdomains that can be identified by naming conventions.

[[business:Subdomain]]
.Creates predefined Subdomain nodes and connects them to all Type nodes via naming conventions.
[source,cypher,role=concept]
----
UNWIND [
    { name: "Clinic" },
    { name: "Owner" },
    { name: "Person" }, 
    { name: "Pet" },
    { name: "Specialty" },
    { name: "Vet" }, 
    { name: "Visit" }
]
AS properties
MERGE (s:Business:Subdomain{name:properties.name})
WITH s
    MATCH (t:Type:Domain)
        WHERE t.name CONTAINS s.name
    MERGE (t)-[:BELONGS_TO]->(s)
RETURN s.name as Subdomain, COUNT(t) as Types
----

==== Dependencies between subdomains

There are dependencies between business sub domains.

[[business:BusinessDependency]]
.Creates DEPENDS_ON relations between two subdomains if there are Java type dependencies between them.
[source,cypher,role=concept]
----
MATCH
  (s1:Business:Subdomain)<-[:BELONGS_TO]-(t1:Type:Domain)-[d:DEPENDS_ON]->(t2:Type:Domain)-[:BELONGS_TO]->(s2:Business:Subdomain)
WHERE
  s1 <> s2
WITH
  s1, s2, count(d) as weight
MERGE
  (s1)-[d:DEPENDS_ON]->(s2)
SET
  d.weight = weight
RETURN
  s1.name as Business, collect(s2.name) as Dependencies
ORDER BY
  Business
----



==== Technical Aspects

The application consists of several business sub domains that can be identified by package naming conventions.

[[business:TechnicalAspects]]
.Creates information about technical aspects using package naming conventions.
[source,cypher,role=concept]
----
MATCH (p:Package)-[:CONTAINS]->(t:Type:Domain)
WITH t, p.name as packageName 
MERGE (ta:Business:TechnicalAspect {name: packageName})
MERGE (ta)<-[:IS_A]-(t)
RETURN ta.name as TechnicalAspect, count(t) as Types
----

==== Dependencies between technical aspects

There are dependencies between business technical aspects.

[[business:TechnicalAspectsDependency]]
.Creates DEPENDS_ON relations between two subdomains if there are Java type dependencies between them.
[source,cypher,role=concept]
----
MATCH
  (t1:Type)-[:IS_A]->(ta1:Business:TechnicalAspect),
  (t2:Type)-[:IS_A]->(ta2:Business:TechnicalAspect),
  (t1)-[d:DEPENDS_ON]->(t2)
WHERE
  ta1 <> ta2
WITH
  ta1, ta2, count(d) as weight
MERGE
  (ta1)-[d:DEPENDS_ON]->(ta2)
SET
  d.weight = weight
RETURN
  ta1.name as TechnicalAspect, collect(ta2.name) as Dependencies
ORDER BY
  TechnicalAspect
----

==== Alternative Modularization with Bounded Contexts
This query prepares the scanned data for virtual reengineering activities.

[[business:AlternativeModularization]]
.Connects the values of `@BoundedContext` Java Annotation to the underlying classes.
[source,cypher,role=concept]
----
MATCH
   (a:Artifact { name: "spring-petclinic"})-[:CONTAINS*]->
      (t:Type)-[:ANNOTATED_BY]->(annotation:Annotation)-[:OF_TYPE]->
         (annotationType:Type { name: "BoundedContext"}),
            (annotation)-[:HAS]->(v:Value)
WHERE t.name <> "package-info"
MERGE (ta:BoundedContext)<-[:IS_WITHIN]-(t)
SET
   ta.name = v.value
RETURN DISTINCT v.value as BoundedContext, t.name as Type
ORDER BY v.value, t.name
----
