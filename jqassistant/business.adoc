[[business:Default]]
[role=group,includesConcepts="business:Subdomain,business:Crossfunctional,business:BusinessDependency"]
== Business

This section describes the application from the business' perspective.

=== Concepts

==== Subdomains
The Spring PetClinic application consists of several business sub domains that can be identified by naming conventions.

[[business:Subdomain]]
.Creates predefined Subdomain nodes and connects them to all Type nodes via naming conventions.
[source,cypher,role=concept]
----
UNWIND [
    { name: "Clinic" },
    { name: "Owner" },
    { name: "Person" }, 
    { name: "Pet" },
    { name: "Specialty" },
    { name: "Vet" }, 
    { name: "Visit" }
]
AS properties
MERGE (s:Business:Subdomain{name:properties.name})
WITH s
    MATCH (t:Type)
        WHERE t.name CONTAINS s.name
    MERGE (t)-[:BELONGS_TO]->(s)
RETURN s.name as Subdomain, COUNT(t) as Types
----

==== Crossfunctional Aspects
Some types are not specific to a subdomain, but are there for crossfunctional aspects.
[[business:Crossfunctional]]
.Mark all non-subdomain types for the current Project with a Crossfunctional label.
[source,cypher,role=concept]
----
MATCH (t:Type)<-[:CONTAINS*]-(:Project)
WHERE NOT (t)-[:BELONGS_TO]->(:Business:Subdomain)
MERGE (s:Business:Crossfunctional {name: "crossfunctional"})
MERGE (s)<-[:BELONGS_TO]-(t)
RETURN s.name as Crossfunctional, count(t) as Types
----


There are dependencies between business sub domains.

[[business:BusinessDependency]]
.Creates DEPENDS_ON relations between two subdomains if there are Java type dependencies between them.
[source,cypher,role=concept,requiresConcepts="business:Subdomain, business:Crossfunctional"]
----
MATCH
  (t1:Type)-[:BELONGS_TO]->(s1:Business),
  (t2:Type)-[:BELONGS_TO]->(s2:Business),
  (t1)-[d:DEPENDS_ON]->(t2)
WHERE
  s1 <> s2
WITH
  s1, s2, count(d) as weight
MERGE
  (s1)-[d:DEPENDS_ON]->(s2)
SET
  d.weight = weight
RETURN
  s1.name as Business, collect(s2.name) as Dependencies
ORDER BY
  Business
----
