[[structure:Default]]
[role=group,includesConstraints="dependency:PackageCycles",includesConcepts="structure:PackageDependencies.graphml,structure:ClassRoleDependencies.graphml"]
== Structural Rules And Reports

The following constraints are verified:

- Package cycles

The following GraphML reports are created:

- <<structure:PackageDependencies.graphml>>
- <<structure:ClassRoleDependencies.graphml>>

=== Reports

TIP: GraphML reports may be viewed using http://www.yworks.com/en/products/yfiles/yed/[yEd]. After opening a file you
should apply a layout, e.g. "Layout->Hierarchical".

[[structure:PackageDependencies.graphml]]
[source,cypher,role=concept]
.Dependencies between packages and their contained types.
----
MATCH
  (package:Package)-[:CONTAINS]->(type:Type)
OPTIONAL MATCH
  (type)-[dependsOn:DEPENDS_ON]->(:Type)
RETURN {
  role : "graph",
  parent : package,
  nodes : collect(type),
  relationships : collect(dependsOn)
}
----


[[structure:ClassRoleDependencies.graphml]]
[source,cypher,role=concept,requiresConcepts="spring:Controller,spring:Service,spring:Repository,java:TypeAssignableFrom"]
.Actual usage dependencies between controller, service and repository implementations.
----
WITH
  [
    "Controller",
    "Service",
    "Repository"
  ] as selectedRoles
MATCH
  (type:Type:Spring)-[:DECLARES]->(:Method)-[:INVOKES]->(:Method)<-[:DECLARES]-(:Type:Interface)<-[:IMPLEMENTS|EXTENDS*]-(otherType:Type:Spring)
WHERE
  type <> otherType
  and any (role in selectedRoles WHERE role in labels(type))
  and any (role in selectedRoles WHERE role in labels(otherType))
WITH DISTINCT
  type, otherType
RETURN
  type,
  {
    role : "relationship",
    startNode : type,
    endNode : otherType,
    type : "DEPENDS_ON"
  },
  otherType
----
