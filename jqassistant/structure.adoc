[[structure:Default]]
[role=group,includesConstraints="structure:ControllerDependencies,structure:ServiceDependencies,structure:RepositoryDependencies,structure:ImplementationDependencies,dependency:PackageCycles",includesConcepts="structure:PackageDependencies.graphml,structure:LayerDependencies.graphml"]
== Structural Rules And Reports

The following constraints are verified:

- <<structure:ControllerDependencies>>
- <<structure:ServiceDependencies>>
- <<structure:RepositoryDependencies>>
- <<structure:ImplementationDependencies>>
- Package cycles

The following GraphML reports are created:

- <<structure:LayerDependencies.graphml>>
- <<structure:PackageDependencies.graphml>>

=== Constraints

The application consists of the following layers:

- UI: Controller implementations
- Logic: Service implementations
- Persistence: Repositories (JDBC, JPA or Spring Data)

The following sections describe restrictions on dependencies between these layers.


==== General

[[structure:ImplementationDependencies]]
[source,cypher,role=constraint,requiresConcepts="spring-mvc:Controller,spring-mvc:Service,spring-data:Repository"]
.There must be no direct dependencies between controller, service and repository implementations, i.e. only dependencies to interfaces are allowed.
----
WITH
  [
    "Controller",
    "Service",
    "Repository"
  ] as selectedRoles
MATCH
  (type:Spring:Type)-[:DECLARES]->(:Method)-[:INVOKES]->(:Method)<-[:DECLARES]-(otherType:Spring:Type)
WHERE
  type <> otherType
  and any (selectedRole in selectedRoles WHERE selectedRole in labels(type))
  and any (selectedRole in selectedRoles WHERE selectedRole in labels(otherType))
  and not (otherType:Interface)
RETURN DISTINCT
  type as Dependent, otherType as InvalidDependency
----


==== UI

[[structure:ControllerDependencies]]
[source,cypher,role=constraint,requiresConcepts="structure:LayerDependencies"]
.A controller can only have dependencies to services (e.g. direct usage of repositories is not allowed).
----
MATCH
  (type:Type:Spring:Controller)-[:USES]->(otherType:Type:Spring)
WHERE NOT
  otherType:Service
RETURN DISTINCT
  type as InvalidController, otherType as Dependency
----


==== Logic

[[structure:ServiceDependencies]]
[source,cypher,role=constraint,requiresConcepts="structure:LayerDependencies"]
.A service can only have dependencies to repositories or other services (e.g. usage of controllers is not allowed).
----
MATCH
  (type:Type:Spring:Service)-[:USES]->(otherType:Type:Spring)
WHERE NOT (
  otherType:Service
  or otherType:Repository
)
RETURN DISTINCT
  type as InvalidService, otherType as Dependency
----


==== Persistence

[[structure:RepositoryDependencies]]
[source,cypher,role=constraint,requiresConcepts="structure:LayerDependencies"]
.A repository can only have dependencies to other repositories.
----
MATCH
  (type:Type:Spring:Repository)-[:USES]->(otherType:Type:Spring)
WHERE NOT
  otherType:Repository
RETURN DISTINCT
  type as InvalidRepository, otherType as Dependency
----


=== Concepts

[[structure:LayerDependencies]]
[source,cypher,role=concept,requiresConcepts="spring-mvc:Controller,spring-mvc:Service,spring-data:Repository"]
.Creates a USES relation between application layer items, i.e. controller, service and repository implementations.
----
WITH
  [
    "Controller",
    "Service",
    "Repository"
  ] as selectedRoles
MATCH
  (type:Spring:Type)-[:DECLARES]->(:Method)-[:INVOKES]->(:Method)<-[:DECLARES]-(:Type:Interface)<-[:IMPLEMENTS|EXTENDS*]-(otherType:Spring:Type)
WHERE
  type <> otherType
  and any (selectedRole in selectedRoles WHERE selectedRole in labels(type))
  and any (selectedRole in selectedRoles WHERE selectedRole in labels(otherType))
MERGE
  (type)-[:USES]->(otherType)
RETURN
  type as Dependent, collect(distinct otherType.fqn) as Dependencies
----


=== Reports

TIP: GraphML reports may be viewed using http://www.yworks.com/en/products/yfiles/yed/[yEd]. After opening a file you
should apply a layout, e.g. "Layout->Hierarchical".


[[structure:LayerDependencies.graphml]]
[source,cypher,role=concept,requiresConcepts="structure:LayerDependencies"]
.Actual usage dependencies between the application layers, i.e. controller, service and repository implementations.
----
MATCH
  (type:Spring:Type)-[uses:USES]->(otherType:Spring:Type)
RETURN
  type as Dependent,
  uses as Uses,
  otherType as Dependency
----


[[structure:PackageDependencies.graphml]]
[source,cypher,role=concept]
.Dependencies between packages and their contained types.
----
MATCH
  (package:Package)-[:CONTAINS]->(type:Type)
OPTIONAL MATCH
  (type)-[dependsOn:DEPENDS_ON]->(:Type)
RETURN {
  role : "graph",
  parent : package,
  nodes : collect(type),
  relationships : collect(dependsOn)
} as TypesPerPackage
----
