[[structure:Default]]
[role=group,includesConstraints="dependency:PackageCycles",includesConstraints="structure:ImplementationDependency,dependency:PackageCycles",includesConcepts="structure:PackageDependencies.graphml,structure:LayerDependencies.graphml"]
== Structural Rules And Reports

The following constraints are verified:

- <<structure:ImplementationDependency>>
- Package cycles

The following GraphML reports are created:

- <<structure:LayerDependencies.graphml>>
- <<structure:PackageDependencies.graphml>>

=== Constraints

[[structure:ImplementationDependency]]
[source,cypher,role=constraint,requiresConcepts="spring:Controller,spring:Service,spring:Repository"]
.There must be no direct dependencies between controller, service and repository implementations, i.e. only dependencies to interfaces are allowed.
----
WITH
  [
    "Controller",
    "Service",
    "Repository"
  ] as selectedRoles
MATCH
  (type:Type)-[:DECLARES]->(:Method)-[:INVOKES]->(:Method)<-[:DECLARES]-(otherType:Type)
WHERE
  type <> otherType
  and any (selectedRole in selectedRoles WHERE selectedRole in labels(type))
  and any (selectedRole in selectedRoles WHERE selectedRole in labels(otherType))
  and not (otherType:Interface)
RETURN DISTINCT
  type as Dependent, otherType as InvalidDependency
----

=== Concepts

[[structure:LayerDependencies]]
[source,cypher,role=concept,requiresConcepts="spring:Controller,spring:Service,spring:Repository"]
.Creates a USES relation between application layer items, i.e. controller, service and repository implementations.
----
WITH
  [
    "Controller",
    "Service",
    "Repository"
  ] as selectedRoles
MATCH
  (type:Type)-[:DECLARES]->(:Method)-[:INVOKES]->(:Method)<-[:DECLARES]-(:Type:Interface)<-[:IMPLEMENTS|EXTENDS*]-(otherType:Type)
WHERE
  type <> otherType
  and any (selectedRole in selectedRoles WHERE selectedRole in labels(type))
  and any (selectedRole in selectedRoles WHERE selectedRole in labels(otherType))
MERGE
  (type)-[:USES]->(otherType)
RETURN
  type as Dependent, collect(distinct otherType.fqn) as Dependencies
----


=== Reports

TIP: GraphML reports may be viewed using http://www.yworks.com/en/products/yfiles/yed/[yEd]. After opening a file you
should apply a layout, e.g. "Layout->Hierarchical".


[[structure:LayerDependencies.graphml]]
[source,cypher,role=concept,requiresConcepts="structure:LayerDependencies"]
.Actual usage dependencies between the application layers, i.e. controller, service and repository implementations.
----
MATCH
  (type:Type)-[uses:USES]->(otherType:Type)
WHERE
  type <> otherType
RETURN
  type as Dependent,
  uses as Uses,
  otherType as Dependency
----


[[structure:PackageDependencies.graphml]]
[source,cypher,role=concept]
.Dependencies between packages and their contained types.
----
MATCH
  (package:Package)-[:CONTAINS]->(type:Type)
OPTIONAL MATCH
  (type)-[dependsOn:DEPENDS_ON]->(:Type)
RETURN {
  role : "graph",
  parent : package,
  nodes : collect(type),
  relationships : collect(dependsOn)
}
----
