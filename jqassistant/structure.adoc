[[structure:Default]]
[role=group,includesConstraints="dependency:PackageCycles",includesConstraints="structure:ImplementationDependency,dependency:PackageCycles",includesConcepts="structure:PackageDependencies.graphml,structure:ClassRoleDependencies.graphml"]
== Structural Rules And Reports

The following constraints are verified:

- <<structure:ImplementationDependency>>
- Package cycles

The following GraphML reports are created:

- <<structure:PackageDependencies.graphml>>
- <<structure:ClassRoleDependencies.graphml>>

=== Constraints

[[structure:ImplementationDependency]]
[source,cypher,role=constraint,requiresConcepts="spring:Controller,spring:Service,spring:Repository"]
.There must be no direct dependencies between controller, service and repository implementations, i.e. only dependencies to interfaces are allowed.
----
WITH
  [
    "Controller",
    "Service",
    "Repository"
  ] as selectedRoles
MATCH
  (type:Type)-[:DECLARES]->(:Method)-[:INVOKES]->(:Method)<-[:DECLARES]-(otherType:Type)
WHERE
  type <> otherType
  and any (selectedRole in selectedRoles WHERE selectedRole in labels(type))
  and any (selectedRole in selectedRoles WHERE selectedRole in labels(otherType))
  and not (otherType:Interface)
RETURN
  type, otherType
----

=== Reports

TIP: GraphML reports may be viewed using http://www.yworks.com/en/products/yfiles/yed/[yEd]. After opening a file you
should apply a layout, e.g. "Layout->Hierarchical".

[[structure:PackageDependencies.graphml]]
[source,cypher,role=concept]
.Dependencies between packages and their contained types.
----
MATCH
  (package:Package)-[:CONTAINS]->(type:Type)
OPTIONAL MATCH
  (type)-[dependsOn:DEPENDS_ON]->(:Type)
RETURN {
  role : "graph",
  parent : package,
  nodes : collect(type),
  relationships : collect(dependsOn)
}
----


[[structure:ClassRoleDependencies.graphml]]
[source,cypher,role=concept,requiresConcepts="spring:Controller,spring:Service,spring:Repository"]
.Actual usage dependencies between controller, service and repository implementations.
----
WITH
  [
    "Controller",
    "Service",
    "Repository"
  ] as selectedRoles
MATCH
  (type:Type)-[:DECLARES]->(:Method)-[:INVOKES]->(:Method)<-[:DECLARES]-(:Type:Interface)<-[:IMPLEMENTS|EXTENDS*]-(otherType:Type)
WHERE
  type <> otherType
  and any (selectedRole in selectedRoles WHERE selectedRole in labels(type))
  and any (selectedRole in selectedRoles WHERE selectedRole in labels(otherType))
WITH DISTINCT
  type, otherType
RETURN
  type,
  {
    role : "relationship",
    startNode : type,
    endNode : otherType,
    type : "DEPENDS_ON"
  },
  otherType
----
